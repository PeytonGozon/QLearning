(game "Amazons"
    (players 2)
    (equipment
        {
            (board (square 10))
            // This allows a queen to move once, and then move a second time.
            // Queens get the traditional queen movement.
            (piece "Queen" Each (move Slide (then (moveAgain))))
            (piece "Dot" Neutral)
        }
    )
    (rules
        // This adds in the initial queens. "Queen" Each from above
        // implicitly adds Queen{i} for each player.
        (start
            {
                (place "Queen1" {"A4" "D1" "G1" "J4"})
                (place "Queen2" {"A7" "D10" "G10" "J7"})
            }
        )


        (play
            // If the turn is even (0, 2, 4, ...) we allow a queen to move
            // according to (forEach Piece)
            // On odd turns, a queen can only then shoot a dot into any empty position.
            (if (is Even (count Moves))
                // This says that Ludii should determine all legal moves for a player
                (forEach Piece)
                // Allow the user to shoot
                (move Shoot (piece "Dot0"))
            )
        )

        // We want the person who last made a move to win the game whenever
        // the next player has no move.
        (end
            (if
                (no Moves Next)
                (result Mover Win)
            )
        )
    )
)

// these are for improving graphics
(metadata
    (graphics
        {
            (piece Scale "Dot" 0.333)
            (board Style Chess)
        }
    )
)